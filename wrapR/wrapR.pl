### Takes a bunch of filenames, usually generated by make
### Generates an R wrapper that:
### ** loads all .RData files (and .RData files corresponding to .Rout files)
### ** Does something tricky with environment files
### ** Puts unrecognized file names into a variable called input_files
### ** sources all .R files (verbosely)
### ** saves image to a .RData file

### Make should pipe output to a .Rout file,
### and provide its name as first argument.

use strict;
use 5.10.0;

my $useCLArgs = "for (a in commandArgs(TRUE)){ eval(parse(text=a)) }";

my @env;
my @envir;
my @R;
my @input;

my $target = shift(@ARGV);
die "ERROR -- Rprep.pl: Illegal target $target (does not end with .Rout) \n" unless $target =~ s/.Rout$/.RData/;
die "ERROR -- Rprep.pl: No input files received, nothing to do.  A rule, script or dependency is probably missing from the project directory \n" unless @ARGV>0;

my $rtarget = $target;
$rtarget =~ s/\.RData//;

my $savetext = "save.image(file=\"$target\")";
my $save = $savetext;

foreach(@ARGV){
	s/Rout$/RData/;
	if ((/\.RData$/) or (/\.rda$/) or (/.R.env$/) or (/.Rdata/)){
		push @env, $_;
	} elsif (/\.R$/){
		push @R, $_;
	} elsif (/\.envir$/){
		s/.envir//;
		s/Rout$/RData/;
		push @envir, "\"$_\"";
	} else {
		push @input, "\"$_\"";
	}
}

foreach(@env){
	say "load('$_')";
}
say;

if (@input){
	print "\ninput_files <- c(";
	print join ", ", @input;
	print ")\n";
}

if (@envir){
	print "\nenvir_list <- list(); ";
	print "for (f in  c(";
	print join ", ", @envir;
	say ")){envir_list[[f]] <- new.env(); load(f, envir=envir_list[[f]])}";
}

print "pdf(\"$rtarget.Rout.pdf\")\n\n";

foreach my $f (@R){
	say "source('$f', echo=TRUE)";
	my $text;
	open (INF, $f);
	while(<INF>){
		if (/rdsave/){
			$save = $_;
			$save =~ s/^# *//;
			if ($save =~/^#/) {$save=$savetext} else{
				$save =~ s/rdsave\s*\(/save(file="$target", / or die("Problem with special statement $save");
			}
		}

		if (/rdnosave/){
			$save = "";
		}
	}
}

print "\n# If you see this in your terminal, the R script $rtarget.wrap.R did not close properly\n$save\n";

